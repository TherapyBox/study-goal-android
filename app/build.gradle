apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'

buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
        jcenter()
    }

    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
        classpath 'io.fabric.tools:gradle:1.22.1'
    }
}

repositories {
    mavenCentral()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    maven { url "https://jitpack.io" }
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.studygoal.jisc"
        minSdkVersion 17
        targetSdkVersion 25
        versionCode 10
        versionName "1.1.0"
        multiDexEnabled true
        setProperty("archivesBaseName", "studygoal-app")
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    dataBinding {
        enabled true
    }

    dexOptions {
        javaMaxHeapSize "6g"
    }
}

dependencies {
    // Include libs folder
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile files('libs/HockeySDK-3.6.0.jar')

    // Android libs
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:cardview-v7:25.3.1'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:design:25.3.1'

    // RxJava
    compile 'io.reactivex:rxjava:1.2.0'
    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'com.jakewharton.rxbinding:rxbinding:0.4.0'

    // Facebook Sdk
    compile 'com.facebook.android:facebook-android-sdk:[4,5)'

    // Google service
    compile 'com.google.firebase:firebase-messaging:11.0.1'
    compile 'com.google.firebase:firebase-auth:11.0.1'
    compile 'com.google.android.gms:play-services-auth:11.0.1'

    // Twitter sdk
    compile('com.twitter.sdk.android:twitter:2.3.1@aar') {
        transitive = true;
    }

    // Barchart
    compile 'com.github.PhilJay:MPAndroidChart:v3.0.2'

    // For network connection
    compile 'com.squareup.okhttp3:okhttp:3.8.1'
    compile 'com.squareup.okhttp3:logging-interceptor:3.8.1'
    compile 'com.github.bumptech.glide:okhttp3-integration:1.5.0@aar'

    // Event bus
    compile 'org.greenrobot:eventbus:3.0.0'

    // Custom libs
    compile 'com.github.bumptech.glide:glide:3.8.0'
    compile 'com.michaelpardo:activeandroid:3.1.0-SNAPSHOT'
    compile 'com.github.AndroidDeveloperLB:AutoFitTextView:4'

    // For testing
    testCompile 'junit:junit:4.12'
}

android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def outputFile = output.outputFile
        if (outputFile != null && outputFile.name.endsWith('.apk')) {
            def fileName = outputFile.name.replace('.apk', "-" + versionCode + ".apk")
            output.outputFile = new File(outputFile.parent, fileName)
        }
    }
}

apply plugin: 'com.google.gms.google-services'